#---------------------------------------------------------------------------*
#                                                                           *
#  GOIL template scanner definition                                         *
#                                                                           *
#  Copyright (C) 2009, ..., 2009 Pierre Molinaro.                           *
#                                                                           *
#  e-mail : molinaro@irccyn.ec-nantes.fr                                    *
#                                                                           *
#  This program is free software; you can redistribute it and/or modify it  *
#  under the terms of the GNU General Public License as published by the    *
#  Free Software Foundation.                                                *
#                                                                           *
#  This program is distributed in the hope it will be useful, but WITHOUT   *
#  ANY WARRANTY; without even the implied warranty of MERCHANDIBILITY or    *
#  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for *
#   more details.                                                           *
#                                                                           *
#---------------------------------------------------------------------------*

template lexique template_scanner {

template "%" ... "%"

replace "\\%" -> "%"

replace "\\n" -> "\n"

replace "\\\\" -> "\\"

@string identifierString
@sint64 sint64value
@string tokenString
@string functionContent

style keywordsStyle -> "Keywords"
style delimitersStyle -> "Delimiters"
style nonTerminalStyle -> "Non Terminal Symbols"
style integerStyle -> "Integer Constants"
style float_csts -> "Floating Point Constants"
style stringStyle -> "String literals"
style commentStyle -> "Comments"

# -------------------------- identifier or lkey word --------------------
# keywords can be written in lower or upper case
# for identifiers, case is significant

$identifier$ ! tokenString error message "an identifier"
$functionContent$ ! functionContent error message "a function definition"

list goilTemplateKeyWordList style keywordsStyle error message "the '%K' keyword"{
  "after",
  "before",
  "between",
  "block",
  "by",
  "call",
  "default",
  "display",
  "do",
  "else",
  "elsif",
  "emptylist",
  "emptymap",
  "end",
  "error",
  "exists",
  "false",
  "for",
  "foreach",
  "from",
  "function",
  "here",
  "if",
  "in",
  "list",
  "listof",
  "let",
  "loop",
  "mapof",
  "mod",
  "no",
  "not",
  "or",
  "prefixedby",
  "return",
  "sort",
  "template",
#  "templateresult",
  "then",
  "to",
  "true",
  "typeof",
  "yes",
  "warning",
  "while",
  "write"}

message unterminatedFunction : "unterminated function definition"

rule '{' {
  repeat while '\u0001' -> '\u007C' | '\u007E' -> '\uFFFD' :
    enterCharacterIntoString ( !?functionContent !*)
  end
  select
    case '}':
      send $functionContent$
    default
      error unterminatedFunction
  end  
}

rule 'a'->'z' |  'A'->'Z' {
  repeat
    enterCharacterIntoString ( !?identifierString !*)
    enterCharacterIntoString ( !?tokenString !*)
  while 'a'->'z' | 'A'->'Z' | '_' | '0'->'9' :
  end
  send search identifierString in goilTemplateKeyWordList default $identifier$
}

#------------------------------- Integer, floating Point Constant ------------------
@double floatValue
$literal_double$ !floatValue !tokenString  style float_csts error message "a float number"
$signed_literal_integer64$ ! sint64value  style integerStyle error message "a 64-bit signed decimal number"

message decimalNumberTooLarge : "decimal number too large"
message floatNumberConversionError : "invalid float number"
message internalError : "internal error"

rule '-' {
  select
  case '0'->'9':
    enterCharacterIntoString ( !?tokenString !'-')
    enterCharacterIntoString ( !?tokenString !*)
    repeat
    while '0'->'9' :
      enterCharacterIntoString ( !?tokenString !*)
    while '_' :
    end
    select
    case '.' :
      enterCharacterIntoString ( !?tokenString !'.')
      repeat
      while '0'->'9' :
        enterCharacterIntoString ( !?tokenString !*)
      while '_' :
      end
      convertStringToDouble ( !tokenString !?floatValue error floatNumberConversionError)
      send $literal_double$
    default
      convertDecimalStringIntoSInt64 ( !tokenString !?sint64value error decimalNumberTooLarge, internalError)
      send $signed_literal_integer64$
    end
  default
    send $-$
  end
}

rule '0'->'9' {
  enterCharacterIntoString ( !?tokenString !*)
  repeat
  while '0'->'9' :
    enterCharacterIntoString ( !?tokenString !*)
  while '_' :
  end
  select
  case '.' :
    enterCharacterIntoString ( !?tokenString !'.')
    repeat
    while '0'->'9' :
      enterCharacterIntoString ( !?tokenString !*)
    while '_' :
    end
    convertStringToDouble ( !tokenString !?floatValue error floatNumberConversionError)
    send $literal_double$
  default
    convertDecimalStringIntoSInt64 ( !tokenString !?sint64value error decimalNumberTooLarge, internalError)
    send $signed_literal_integer64$
  end
}


#------------------------------- Floating point Number ------------------
$.$  style delimitersStyle error message "the '.' delimitor"
$.=$  style delimitersStyle error message "the '.=' delimitor"
$...$  style delimitersStyle error message "the '...' delimitor"

#--- Floating Point constant begining with a dot
rule '.' {
  select
  case '0'->'9' :
    enterCharacterIntoString ( !?tokenString !'0')
    enterCharacterIntoString ( !?tokenString !'.')
    enterCharacterIntoString ( !?tokenString !*)
    repeat
    while '0'->'9' :
      enterCharacterIntoString ( !?tokenString !*)
    while '_' :
    end
    convertStringToDouble ( !tokenString !?floatValue error floatNumberConversionError)
    send $literal_double$
  default
    select
    case ".." :
      send $...$
    case "=" :
      send $.=$
    default
      send $.$
    end
  end
}

# ----------------------------- Delimitors ---------------------------------------
list galgasDelimitorsList style delimitersStyle error message "the '%K' delimitor"{
  "*",     "|",    ",",      "+",     "--",
  "::",    ">",    ";",      ":",
  "-",     "(",    ")",     "->",      "?",
  "==",   "??",    "!",     ":=",     "++",
  "[",     "]",   "+=",     "?!",     "!?",
  "/",    "!=",   ">=",      "&",
  "{",     "}",   "+>",     "-=",
  "^",    ">>",    "~",     "<-",
  "@"}

rule list galgasDelimitorsList


# --------- Special delimitors / non terminal symbol --------------------------
$<$  style delimitersStyle error message "the '<' delimitor"
$<=$  style delimitersStyle error message "the '<=' delimitor"
$<<$  style delimitersStyle error message "the '<<' delimitor"

rule '<' {
  select
  case '=' :
    send $<=$
  case '<' :
    send $<<$
  default
    send $<$
  end
}

# ------------------------------------ String ----------------------------

message unterminatedLitteralString : "Unterminated literal string" 

@string a_string

$string$ !a_string  style stringStyle error message "literal string"

rule '"' {
    repeat
    while '\u0020'->'\u0021' | '\u0023' -> '\uFFFD' :
        enterCharacterIntoString ( !?a_string !*)
    end
    select
        case '"' :
            send $string$
        default
            error unterminatedLitteralString
    end
}

# ------------------------------------ Comment ----------------------------
$comment$  style commentStyle error message "a comment"
rule '#' {
  repeat
  while '\u0001' -> '\u0009' | '\u000B' | '\u000C' | '\u000E' -> '\uFFFD' :
  end
  drop $comment$
}

# --------------------- separators -----------------------------------------
rule '\u0001' -> ' ' {
}

}
